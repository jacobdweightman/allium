type Nat {
    ctor Zero;
    ctor S(Nat);
}

pred eqNat(Nat, Nat) {
    eqNat(Zero, Zero) <- true;
    eqNat(S(let x), S(x)) <- true;
}

pred even(Nat) {
    even(Zero) <- true;
    even(S(S(let x))) <- even(x);
}

pred add(Nat, Nat, Nat) {
    add(let x, Zero, x) <- true;
    add(let x, S(let y), S(let z)) <- add(x, y, z);
}

pred mul(Nat, Nat, Nat) {
    mul(_, Zero, Zero) <- true;
    mul(let x, S(let y), let z) <- mul(x, y, let w), add(w, x, z);
}

pred testEven {
    testEven <- even(S(S(S(S(S(S(Zero)))))));
}

pred testAdd {
    testAdd <-
        add(S(S(Zero)), S(S(S(Zero))), S(S(S(S(S(Zero)))))),

        add(S(S(Zero)), S(S(S(Zero))), let five),
        eqNat(five, S(S(S(S(S(Zero)))))),

        add(S(S(Zero)), let three, S(S(S(S(S(Zero)))))),
        eqNat(three, S(S(S(Zero)))),

        add(let two, S(S(S(Zero))), S(S(S(S(S(Zero)))))),
        eqNat(two, S(S(Zero)));
}

pred testMul {
    testMul <-
        mul(S(S(Zero)), S(S(S(Zero))), S(S(S(S(S(S(Zero))))))),

        mul(S(S(Zero)), S(S(S(Zero))), let six),
        eqNat(six, S(S(S(S(S(S(Zero))))))),
        
        mul(S(S(Zero)), let three, S(S(S(S(S(S(Zero))))))),
        eqNat(three, S(S(S(Zero)))),

        mul(let two, S(S(S(Zero))), S(S(S(S(S(S(Zero))))))),
        eqNat(two, S(S(Zero)));
}

pred main {
    main <-
        testEven,
        testAdd,
        testMul;
}
